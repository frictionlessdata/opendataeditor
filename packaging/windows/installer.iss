; This script was generated by the Inno Setup Script Wizard and then slightly modify for ODE requirements.
; Windows installers are tricky and the first versions of ODE were using OOO electron-builder so we might
; be impacted in the way we package for Windows.
;
; TODO: This current packaging method works but Windows will not recognize v1.4.0 and v1.3.0 to be the same application
; and therefore the user will need to uninstall the previous one from the system. Inno Setup provides a better experience
; but former versions where packaged with NSIS.
;
; How to execute
; 'C:\Program Files (x86)\Inno Setup 6\ISCC.exe' /DAppVersion=1.4.1 .\packaging\windows\installer.iss

#define AppName "Open Data Editor"
#define AppPublisher "Open Knowledge Foundation"
#define AppURL "https://opendataeditor.okfn.org"
; AppExeName must match the PyInstaller generated file
#define AppExeName "opendataeditor.exe"

[Setup]
; NOTE: The value of AppId uniquely identifies this application.
; We are using the same AppId that Electron Builder set in previous versions of the application which is a
; UUID v5 generated from org.opendataeditor.app. See: https://www.electron.build/nsis.html#guid-vs-application-name
; Inno Setup will append a _is1 to it in the Uninstall Registry: https://jrsoftware.org/ishelp/index.php?topic=setup_appid
AppId=42c092cd-67f7-566d-b9a4-980d3103f082
AppName={#AppName}
AppVersion={#AppVersion}
AppPublisher={#AppPublisher}
AppPublisherURL={#AppURL}
AppSupportURL={#AppURL}
AppUpdatesURL={#AppURL}
DefaultDirName={autopf}\opendataeditor
UninstallDisplayIcon={app}\{#AppExeName}
DisableProgramGroupPage=yes
; Remove the following line to run in administrative install mode (install for all users).
PrivilegesRequired=lowest
; #AppVersion is passed as a parameter of the iscc command.
OutputBaseFilename=opendataeditor-win-{#AppVersion}
OutputDir=..\..\dist
SetupIconFile=icon.ico
SolidCompression=yes
WizardStyle=modern

[Languages]
Name: "english"; MessagesFile: "compiler:Default.isl"
Name: "french"; MessagesFile: "compiler:Languages\French.isl"
Name: "portuguese"; MessagesFile: "compiler:Languages\Portuguese.isl"
Name: "spanish"; MessagesFile: "compiler:Languages\Spanish.isl"

[Tasks]
Name: "desktopicon"; Description: "{cm:CreateDesktopIcon}"; GroupDescription: "{cm:AdditionalIcons}"; Flags: unchecked

[Files]
Source: "..\..\dist\opendataeditor\*"; DestDir: "{app}"; Flags: ignoreversion recursesubdirs createallsubdirs

[Icons]
Name: "{autoprograms}\{#AppName}"; Filename: "{app}\{#AppExeName}"
Name: "{autodesktop}\{#AppName}"; Filename: "{app}\{#AppExeName}"; Tasks: desktopicon

[Run]
Filename: "{app}\{#AppExeName}"; Description: "{cm:LaunchProgram,{#StringChange(AppName, '&', '&&')}}"; Flags: nowait postinstall skipifsilent
